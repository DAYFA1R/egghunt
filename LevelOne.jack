class LevelOne {
    field int GOAL_X, GOAL_Y;
    field Player player;

    constructor LevelOne new() {
        let GOAL_X = 30;
        let GOAL_Y = 9;
        let player = Player.new(1,14);
        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void renderEnvironment() {
        // draw the bottom floor
        do HelperLib.createFloor(0, 15, 32);

        // draw the top floor
        do HelperLib.createFloor(13, 10, 19);

        // Draw Ladder 1
        do HelperLib.createLadder(12, 14, 5);
        
        do SpriteEgg.draw(GOAL_X, GOAL_Y);

        return;
    }

    method void render() {
        do player.clear();
        do renderEnvironment();
        do player.render();
        return;
    }

    method Player getPlayer() {
        return player;
    }

    method boolean hitTest(int checkCondition) {
        // DEBUG purposes
        // do Output.printInt(player.getX());
        // do Output.printString(",");
        // do Output.printInt(player.getY());
        // do Output.printString(" ");
        // end of DEBUG

        // Check win condition
        if (checkCondition=0) {
            // If player on goal
            if ((player.getX() = GOAL_X) & (player.getY() = GOAL_Y)) {
                return true;
            } else {
                
            }
        }

        // Moving Up
        if (checkCondition=1) {
            // Ladder 1
            if ((player.getX() = 12) & (player.getY() > 9)) {
                return true;
            }
        }

        // Moving Down
        if (checkCondition=2) {
            // Ladder 1
            if (
                (player.getX() = 12) &
                (player.getY() > 8) &
                (player.getY() < 14)
            ) {
                return true;
            }
        }

        // Moving Left
        if (checkCondition=3) {
            if (
                (
                    (player.getY() = 14)
                ) |
                (
                    (player.getY() = 9) &
                    (player.getX() > 12)
                )
            ) {
                return true;
            }
        }

        // Moving Right
        if (checkCondition=4) {
            if ((player.getY() = 14) | (player.getY() = 9)) {
                return true;
            }
        }

        return false;
    }
}
