class LevelTwo {
    field int GOAL_X, GOAL_Y;
    field boolean switchOne, frame;
    field Player player;

    constructor LevelTwo new() {
        let GOAL_X = 30;
        let GOAL_Y = 5;
        let frame = true;
        let switchOne = false;
        let player = Player.new(1,14);
        return this;
    }

    method void dispose() {
        do Memory.deAlloc(this);
        return;
    }

    method void renderEnvironment() {
        // draw the bottom floor
        do HelperLib.createFloor(0, 15, 32);

        // draw the bottom-mid floor
        do HelperLib.createFloor(13, 10, 19);

        // draw the bounding box
        do SpriteBrick.draw(11, 10);
        do SpriteBrick.draw(10, 10);
        do SpriteBrick.draw(9, 10);
        do SpriteBrick.draw(9, 9);
        do SpriteBrick.draw(9, 8);
        do SpriteBrick.draw(9, 7);

        // draw the button
        do HelperLib.createButton(switchOne, 10, 9);

        // Draw Ladder 1
        do HelperLib.createLadder(12, 14, 5);
        
        // Draw top-mid floor
        do HelperLib.createFloor(22, 6, 10);

        // Draw Ladder 2
        do HelperLib.createLadder(21, 9, 4);

        // draw bounding box
        do SpriteBrick.draw(28, 4);
        do SpriteBrick.draw(28, 3);
        do SpriteBrick.draw(29, 3);
        do SpriteBrick.draw(30, 3);
        do SpriteBrick.draw(31, 3);

        // draw the barrier
        do HelperLib.createGate(switchOne, 28, 5, frame);

        // draw the goal
        do SpriteEgg.draw(GOAL_X, GOAL_Y);

        return;
    }

    method void render() {
        do player.clear();
        do renderEnvironment();
        do player.render();
        let frame = HelperLib.toggleBool(frame);
        return;
    }

    method Player getPlayer() {
        return player;
    }

    method boolean hitTest(int checkCondition) {
        // DEBUG purposes
        // do Output.printInt(player.getX());
        // do Output.printString(",");
        // do Output.printInt(player.getY());
        // do Output.printString(" ");
        // end of DEBUG

        // Check win condition
        if (checkCondition=0) {
            // If player on goal
            if ((player.getX() = GOAL_X) & (player.getY() = GOAL_Y)) {
                return true;
            } else {
                
            }
        }

        // Moving Up
        if (checkCondition=1) {
            // Ladder 1
            if ((player.getX() = 12) & (player.getY() > 9)) {
                return true;
            }
            // Ladder 2
            if ((player.getX() = 21) & (player.getY() > 5)) {
                return true;
            }
        }

        // Moving Down
        if (checkCondition=2) {
            // Ladder 1
            if (
                (player.getX() = 12) &
                (player.getY() > 8) &
                (player.getY() < 14)
            ) {
                return true;
            }
            // Ladder 2
            if (
                (player.getX() = 21) &
                (player.getY() > 4) &
                (player.getY() < 9)
            ) {
                return true;
            }
        }

        // Moving Left
        if (checkCondition=3) {
            if (
                (
                    (player.getY() = 14)
                ) |
                (
                    (player.getY() = 9) &
                    (player.getX() > 11)
                ) |
                (
                    (player.getY() = 5) &
                    (player.getX() > 21)
                )
            ) {
                return true;
            }
        }

        // Moving Right
        if (checkCondition=4) {
            if ((player.getY() = 14) | (player.getY() = 9)) {
                return true;
            }
            if (
                (player.getY() = 5) &
                (player.getX() < 27) &
                (~switchOne)
            ) {
                return true;
            }
            if ((player.getY() = 5) & switchOne) {
                return true;
            }
        }

        // Hitting switch
        if (checkCondition=5) {
            if ((player.getX() = 11) & (player.getY() = 9)) {
                let switchOne = HelperLib.toggleBool(switchOne);
                return true;
            }
        }

        return false;
    }
}
